plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.eclipse.jkube.kubernetes' version '1.13.1'
}

group = 'com.asr'
version = '0.0.1-SNAPSHOT'
description = 'Provides API gateway and cross-cutting concerns.'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2022.0.3")
    set('jkube.createExternalUrls', "true")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-reactor-resilience4j'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    implementation 'org.springframework.session:spring-session-data-redis'

    // Some warnings related to DNS resolutions in Netty. Only on Apple silicon.
    runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.1.79.Final:osx-aarch_64'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.testcontainers:junit-jupiter'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

kubernetes {
    image {
        name = "anuragrajawat/${project.name}:${project.version}"
    }

    resources {
        controller {
            // TODO: Move below env vars to configmap
            env {
                SPRING_DATA_REDIS_HOST = 'store-redis'
                CATALOG_SERVICE_URL = 'http://catalog-service'
                ORDER_SERVICE_URL = 'http://order-service'
            }
        }

        services = [{
                        name = "${project.name}"
                        headless = "false"
                        type = "ClusterIP"
                        ports = [{
                                     protocol = "tcp"
                                     port = "80"
                                     targetPort = "9000"
                                 }]
                    }]

        // TODO: Add ingress tls and host configs
        ingress {
            ingressRules = [{
                                paths = [{
                                             pathType = "Prefix"
                                             path = "/"
                                             serviceName = "edge-service"
                                             servicePort = "80"
                                         }]
                            }]
        }
    }
}
